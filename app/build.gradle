plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

def version = "1"
def milestone = "0"
def build = "1"

def versionNum = version.toInteger() * 10000 + milestone.toInteger() * 100 + build.toInteger()
def versionText = version + "." + milestone + "." + build

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "info.megahard.epshowcase"
        minSdkVersion 15
        targetSdkVersion 31
        versionCode versionNum
        versionName versionText
        resValue "string", "app_version", 'v ' + versionText
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [toothpick_registry_package_name:
                                     'info.megahard.epshowcase.toothpick']
            }
        }
    }
    lintOptions {
        abortOnError false
    }
    dataBinding {
        enabled = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }
    buildFeatures {

        viewBinding true
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def newName = name + "-" + versionText + ".apk"
            output.outputFileName = newName
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Support
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.6.0-alpha01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0-alpha01'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'android.arch.navigation:navigation-fragment:1.0.0'
    implementation 'android.arch.navigation:navigation-ui:1.0.0'
    implementation 'androidx.fragment:fragment-ktx:1.4.0'
    implementation 'androidx.multidex:multidex:2.0.1'

    // Toothpick
    implementation 'com.github.stephanenicolas.toothpick:toothpick-runtime:1.1.3'
    kapt "com.github.stephanenicolas.toothpick:toothpick-compiler:1.1.3"

    // Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'

    // Rx
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.0.0'

    // OkHttp
    implementation 'com.squareup.okhttp3:okhttp:3.14.9'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.11.0'

    // FragmentWithArgs
    implementation 'com.hannesdorfmann.fragmentargs:annotation:3.0.2'
    kapt 'com.hannesdorfmann.fragmentargs:processor:3.0.2'

    // Tests
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
